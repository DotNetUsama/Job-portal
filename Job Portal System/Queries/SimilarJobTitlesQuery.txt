SELECT DISTINCT ?title ?id ?distance WHERE {
	{
		SELECT ?term WHERE {
			{ ?term rdfs:label @queryString . }
			UNION
			{
				?t rdfs:label @queryString .
				{
					{ ?t dbo:wikiPageRedirects ?term . }
					UNION
					{ ?t dbo:wikiPageDisambiguates|^dbo:wikiPageDisambiguates ?term . }
				}
			}
		}
	}
	{
		SELECT ?occupationType WHERE {
			?occupationType skos:broader dbc:Occupations_by_type .
			FILTER (?occupationType NOT IN (dbc:Illegal_occupations, dbc:Gendered_occupations, dbc:Indian_castes_by_profession))
		}
	}
	
	{
		{
			?term dct:subject ?occupationType .
			BIND (?term AS ?occupation) .
			BIND ("0" AS ?distance) .
		} UNION {
			?occupation 
				dct:subject|^dct:subject|skos:broader|^skos:broader ?term .
			?occupation dct:subject ?occupationType .
			BIND ("1" AS ?distance) .
		} UNION {
			?occupation 
				dct:subject|^dct:subject|skos:broader|^skos:broader
				[dct:subject|^dct:subject|skos:broader|^skos:broader ?term] .
			?occupation dct:subject ?occupationType .
			BIND ("2" AS ?distance) .
		} UNION {
			?occupation 
				dct:subject|^dct:subject|skos:broader|^skos:broader
				[dct:subject|^dct:subject|skos:broader|^skos:broader
				[dct:subject|^dct:subject|skos:broader|^skos:broader ?term]] .
			?occupation dct:subject ?occupationType .
			BIND ("3" AS ?distance) .
		}
	}
	
	?occupation rdfs:label ?label .
	FILTER (langmatches(lang(?label),"en")) .
	BIND (str(?label) AS ?title) .
	?occupation dbo:wikiPageID ?wikiId .
	BIND (str(?wikiId) AS ?id) .
	FILTER (NOT EXISTS { ?occupation foaf:name [] . }) .
	FILTER (NOT EXISTS { ?occupation <http://purl.org/linguistics/gold/hypernym> dbr:list . }) .
} 